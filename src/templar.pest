WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "{#" ~ (!"#}" ~ ANY)* ~ "#}" }

//A whole template
template = _{ SOI ~ (raw_block | template_block)* ~ EOI }

//Template expressions
expression     = _{ (literal | function | value) ~ operation* }

//Blocks, containing templates or other chunks of data
raw_block      = @{ (!"{{" ~ ANY)+ }
template_block = { "{{" ~ (!"}}" ~ expression) ~ "}}" }
parens_block   = { "(" ~ (expression ~ ("," ~ expression)*)? ~ ")" }

//Operations
operation = _{ ( op_right | filter ) }
filter    = { "|" ~ ident ~ parens_block? }
function  = { ident ~ parens_block }
op_right  = _{ op ~ expression }

//Identifiers
ident     = ${ ASCII_ALPHA ~ ASCII_ALPHANUMERIC*? }
value_key = ${ "[" ~ str_lit ~ "]"}
value_id  = _{ ( "." ~ ident) | value_key }
value     = @{ ident ~ (value_id)* }

//Literals
literal    = _{ (bool_lit | number_lit | str_lit | null_lit) ~ !ASCII_ALPHANUMERIC }
bool_lit   = _{ true_lit | false_lit }
true_lit   = { "true" | "yes" }
false_lit  = { "false" | "no" }
null_lit   = { "null" | "nil" }
number_lit = @{ "-"? ~ ASCII_DIGIT+ }
str_lit   = ${ "'" ~ str_lit_c ~ "'" }
str_lit_c = @{ (!"'" ~ ("\\'" | ANY))* }

// All comparitive operators
op  = _{ op_add | op_sub | op_div | op_mlt | op_mod | op_and | op_or | op_eq | op_ne | op_gt | op_gte | op_lt | op_lte }
op_and = { "&&" }
op_or  = { "||" }
op_eq  = { "==" }
op_ne  = { "!=" }
op_gt  = { ">"  }
op_gte = { ">=" }
op_lt  = { "<"  }
op_lte = { "<=" }
op_add = { "+" }
op_sub = { "-" }
op_div = { "/" }
op_mlt = { "*" }
op_mod = { "%" }
